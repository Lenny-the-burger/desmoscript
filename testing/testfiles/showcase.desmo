"Desmoscript is a programming language that compiles to Desmos expressions!"

"It supports arithmetic expressions"
arithmetic = 1 + 2 - 3 * 4 / 5 * (1 - 2);


"Functions"
fn triple(x) {
    x * 3
}


"Lists"
list = [1,2,3,4,5];


"Ranges"
listRange = 1 .. 5;
everySecondNum = 1,3 .. 5;


"Points"
point = (0, 0);
pointX = point\x;


"List comprehensions"
listComp = [(a, b) for a=[1,2,3]; b=[4,5,6]];


"Namespaced Variables"
ns namespace {
    varInNamespace = 5;
}
varOutOfNamespace = namespace.varInNamespace * 2;


"Piecewises"
fn absolute(x) {
    match {
        (x > 0) => x;
        (-1 * x);
    }
}


"Functions with multiple steps"
fn plusTwo(x) {
    temp = x + 1;
    temp + 1
}


"Integrals and derivatives"
derivativeExample = derivative (x) x * x;
integralExample = integral (n=1 to x) n * n;


"Sums and products"
sumExample = sum (n=1 to 5) n;
prodExample = product (n=1 to 5) n;


"Displayed expressions"
show displayed = x; with @{
    color: "red"
}
show displayedColorLatex = 2 * x; with @{
    color: "black",
    colorLatex: ds(rgb(255, 128, 0))
}



"Graph Settings"
settings @{
    viewport: {
        xmin: -2,
        ymin: -2,
        xmax: 2,
        ymax: 2
    },
    showGrid: false,
    showXAxis: false,
    showYAxis: false
}
